{
  "cells": [
     {"cell_type":"markdown","source":"<h1>Gaussian Quadrature</h1>","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<p>Our goal: approximate the definite integral of $f$ over $[a,b]$ by a sum:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n\\int_a^b f(x) dx = \\sum_{i=0}^n f(x_i) A_i.\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>In fact, we generalize this to include a positive <em>weight</em> function, $w(x)$:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n\\int_a^b f(x) w(x) dx = \\sum_{i=0}^n f(x_i) A_i.\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>With $f(x)$ interpolated by the polynomial $\\sum f(x_i) l_i(x)$, we have</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\nA_i = \\int_a^b w(x) \\prod_{j=0, j \\neq i}^n \\frac{x-x_j}{x_i - x_j} dx\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>.</p>","metadata":{}},
{"cell_type":"markdown","source":"<p>Goal: can we choose the weights in such a way that this is exact for polynomials of a certain degree?</p>","metadata":{}},
{"cell_type":"markdown","source":"<h2>Orthogonal</h2>","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<p>Let $\\Pi_n$ be the polynomials of degree $n$.</p>","metadata":{}},
{"cell_type":"markdown","source":"<p>Say a function $q$ is $w$-orthoganal to $\\Pi_n$ if for any $p \\in \\Pi_n$</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n\\int_a^b q(x) p(x) w(x) dx = 0\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<h3>Example: Legendre polynomials</h3>","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<p>Let $P_0(x) = 1$ and $P_1(x) = x$. Define recursively $P_n(x)$ by:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n(n+1)P_{n+1}(x) = (2n + 1) x P_n(x)  - n P_{n-1}(x).\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>There are the <a href=\"https://en.wikipedia.org/wiki/Legendre_polynomials\">Legendre polynomials</a>. Here are the first few:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["\\begin{bmatrix}1\\\\x\\\\\\frac{3 x^{2}}{2} - \\frac{1}{2}\\\\\\frac{x}{2} \\left(5 x^{2} - 3\\right)\\\\\\frac{35 x^{4}}{8} - \\frac{15 x^{2}}{4} + \\frac{3}{8}\\\\\\frac{x}{8} \\left(63 x^{4} - 70 x^{2} + 15\\right)\\\\\\frac{231 x^{6}}{16} - \\frac{315 x^{4}}{16} + \\frac{105 x^{2}}{16} - \\frac{5}{16}\\end{bmatrix}"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["using SymPy\nx = symbols(\"x\")\nps = Sym[1, x]\nfor n = 1:5\n   pn, pn_1 = ps[end], ps[end-1]\n   p =( (2n+1) * x*pn - n*pn_1 ) * (1// (n+1))\n   push!(ps, simplify(p))\nend\nps"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>Now, take $a=-1$, $b=1$ and $w(x) = 1$. Are these orthogonal? That is, do we have:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n\\int_a^b p_i(x) p_j(x) w(x) dx = \\int_{-1}^1 p_i(x) p_j(x) dx = 0?\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>We can check. For example,</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["\\begin{bmatrix}0\\\\0\\\\0\\\\0\\\\0\\\\0\\end{bmatrix}"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["[integrate(ps[end] * ps[i], (x, -1, 1)) for i in 1:length(ps)-1]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>So $p_i$ is orthogonal to any linear combination of the $p_j, j < i$. But these polynomials span the space of polynomials of degree $i-1$. So they are $w$-orthogonal.</p>","metadata":{}},
{"cell_type":"markdown","source":"<h3>Gaussian quadrature theorem</h3>","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<blockquote>\n<p>(p 493) If $q(x)$ is a non-zero polynomial of degree $n+1$, $w$-orthogonal to $\\Pi_n$ and $x_0, x_1, \\dots, x_n$ are the <em>zeros</em> of $q$, then the quadrature formula will be exact for all functions in $\\Pi_{2n+1}$.</p>\n</blockquote>","metadata":{}},
{"cell_type":"markdown","source":"<p>Before seeing why, let's see that it works with $P_4$:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/plain":["4-element Array{Float64,1}:\n  0.339981\n -0.861136\n -0.339981\n  0.861136"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["p4 = ps[5] # 1/8 *(35x^4 - 30x^2+3)\nxis = collect(keys(polyroots(ps[5])))\nmap(N, xis)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>We define $l_i$ via:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/plain":["l (generic function with 1 method)"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["function l(i)\nout = 1\nn = length(xis)-1 # 0, ..., n => 1, ..., n+1\nfor j in 0:n\n  if j != i\n    out = out * (x-xis[j+1])/(xis[i+1] - xis[j+1])\n  end\nend\nout\nend"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>With these, we define $A_i$ via:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["\\begin{bmatrix}\\frac{\\sqrt{30}}{36} + \\frac{1}{2}\\\\- \\frac{\\sqrt{30}}{36} + \\frac{1}{2}\\\\\\frac{\\sqrt{30}}{36} + \\frac{1}{2}\\\\- \\frac{\\sqrt{30}}{36} + \\frac{1}{2}\\end{bmatrix}"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["Ais = [integrate(l(i), (x, -1,1)) for i in 0:3]"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>Then the claim is $\\sum f(x_i) A_i$ will be exactly the integral $\\int f(x) dx$ for any $7$th degree polynomial or less. Let's try a polynomial:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["$$- \\frac{8}{5}$$"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["f(x) = 5x^5 - 4x^4\nintegrate(f(x), (x, -1, 1))"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>And compare with:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["$$\\left(- \\frac{\\sqrt{30}}{36} + \\frac{1}{2}\\right) \\left(- 5 \\left(\\frac{2 \\sqrt{30}}{35} + \\frac{3}{7}\\right)^{\\frac{5}{2}} - 4 \\left(\\frac{2 \\sqrt{30}}{35} + \\frac{3}{7}\\right)^{2}\\right) + \\left(\\frac{\\sqrt{30}}{36} + \\frac{1}{2}\\right) \\left(- 4 \\left(- \\frac{2 \\sqrt{30}}{35} + \\frac{3}{7}\\right)^{2} - 5 \\left(- \\frac{2 \\sqrt{30}}{35} + \\frac{3}{7}\\right)^{\\frac{5}{2}}\\right) + \\left(\\frac{\\sqrt{30}}{36} + \\frac{1}{2}\\right) \\left(- 4 \\left(- \\frac{2 \\sqrt{30}}{35} + \\frac{3}{7}\\right)^{2} + 5 \\left(- \\frac{2 \\sqrt{30}}{35} + \\frac{3}{7}\\right)^{\\frac{5}{2}}\\right) + \\left(- \\frac{\\sqrt{30}}{36} + \\frac{1}{2}\\right) \\left(- 4 \\left(\\frac{2 \\sqrt{30}}{35} + \\frac{3}{7}\\right)^{2} + 5 \\left(\\frac{2 \\sqrt{30}}{35} + \\frac{3}{7}\\right)^{\\frac{5}{2}}\\right)$$"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["sum([f(xi) * Ais[i] for (i,xi) in enumerate(xis)])"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>Wow, not even close. But let's simplify:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["$$- \\frac{8}{5}$$"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["sum([f(xi) * Ais[i] for (i,xi) in enumerate(xis)]) |> simplify"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>Now let's try for some \"arbitrary\" polynomial:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["$$\\frac{x^{7}}{5040} + \\frac{x^{6}}{720} + \\frac{x^{5}}{120} + \\frac{x^{4}}{24} + \\frac{x^{3}}{6} + \\frac{x^{2}}{2} + x + 1$$"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["f(x) = exp(x)\np = series(f(x), x, n=8) |> removeO # 7th degree poly"],"metadata":{},"execution_count":1},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["$$\\frac{5923}{2520}$$"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["integrate(p, (x, -1, 1))"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>And compare with:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["$$\\frac{5923}{2520}$$"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["sum([subs(p,x,xi) * Ais[i] for (i,xi) in enumerate(xis)]) |> simplify"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>The theorem does <em>not</em> guarantee this is true for 8th degree polynomials:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["$$\\frac{426457}{181440}$$"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["p = series(f(x), x, n=9) |> removeO\nintegrate(p, (x, -1,1))"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>Compare with</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/latex":["$$\\frac{58045529}{24696000}$$"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["sum([subs(p,x,xi) * Ais[i] for (i,xi) in enumerate(xis)]) |> simplify"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<h3>Proof</h3>","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<p>We have $q$ given. Take $f$  a polynomial of degree $2n+1$ or less. Then we can do polynomial long division to write:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\nf(x) = q(x) p(x) + r(x)\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>Where the degree of $r(x)$ is less than that of $q(x) = n+1$. We can evaluation at $x_i$ to see that:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\nf(x_i) = q(x_i) p(x_i) + r(x_i) = 0 \\cdot p(x_i) + r(x_i) = r(x_i).\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>So we get:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n\\int_a^b f(x) w(x) dx = \\int_a^b [(q(x)p(x)w(x)) + (r(x) w(x)) ] dx = \\int_a^b r(x) w(x) dx.\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>And</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n\\sum_{i=0}^n A_i f(x_i) = \\sum_{i=0}^n A_i r(x_i).\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>But, when $A_i$ is defined as it is, it is known the sum is exact for $n$th degree polynomials. So we can equate the two things we just did to get:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n\\int_a^b f(x) w(x) dx  = \\sum_{i=0}^n A_i f(x_i)\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>Which is what was to be shown.</p>","metadata":{}},
{"cell_type":"markdown","source":"<h3>Fact</h3>","metadata":{"internals":{"slide_type":"subslide"},"slideshow":{"slide_type":"subslide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<p>If $q(x)$ is as assumed, it will have real, simple roots all inside $(a,b)$.</p>","metadata":{}},
{"cell_type":"markdown","source":"<p>Why?</p>","metadata":{}},
{"cell_type":"markdown","source":"<blockquote>\n<p>Thm. If $q$ is $w$-orthogonal to $\\Pi_n$, then it has at least $n+1$ sign changes on $(a,b)$.</p>\n</blockquote>","metadata":{}},
{"cell_type":"markdown","source":"<p>First, $1$ is in $\\Pi_n$ and $\\int q(x) 1 w(x) =0$, so $q$ must change sign, as $w > 0$.</p>","metadata":{}},
{"cell_type":"markdown","source":"<p>Suppose, $q$ changes sign only $r$ times, $r \\leq n$. Then one can partition $(a,b)$ by $t_0=a < t_1 < \\cdot < t_r < t_{r+1} = b$ so that $q$ does not change sign on each partition.</p>","metadata":{}},
{"cell_type":"markdown","source":"<p>Form the polynomial $p(x) = (x-t_1) \\cdots (x-t_r)$. Then $p$ is in $\\Pi_n$. and $p$ does not change sign on $(t_i, t_{i+1})$. So</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\n\\int_a^b q(x) p(x) w(x) dx \\neq 0\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>But by orthogonality, this isn't the case, so there is a contradiction and there are more sign changes thatn assumed.</p>","metadata":{}},
{"cell_type":"markdown","source":"<h2>Error</h2>","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<blockquote>\n<p>Theorem 4 (p497)</p>\n</blockquote>","metadata":{}},
{"cell_type":"markdown","source":"<p>Let $E$ be the error:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\nE = \\int_a^b f(x) dx - \\sum_{i=1}^{n-1} A_i f(x_i).\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>Where $f$ is $C^{2n}$. Then the error term can be represented as:</p>","metadata":{}},
{"cell_type":"markdown","source":"\n$$\nE = \\frac{f^{2n}(\\xi)}{(2n)!} \\int_a^b \\prod_{i=0}^{n-1}(x-x^i)^2 w(x) dx.\n$$\n","metadata":{}},
{"cell_type":"markdown","source":"<p>(For polynomials of degree $\\leq 2n-1$ it is clear that $f^{2n}(\\xi) = 0$ and so $E=0$.)</p>","metadata":{}},
{"cell_type":"markdown","source":"<h2>The FastGaussQuadrature package illustrates this</h2>","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<p><a href=\"https://github.com/ajt60gaibb/FastGaussQuadrature.jl\">FastGaussQuadrature</a></p>","metadata":{}},
{"cell_type":"markdown","source":"<h2>The quadgk function</h2>","metadata":{"internals":{"slide_type":"subslide","slide_helper":"subslide_end"},"slideshow":{"slide_type":"slide"},"slide_helper":"slide_end"}},
{"cell_type":"markdown","source":"<p>In <code>Julia</code>, the <a href=\"https://github.com/JuliaLang/julia/blob/master/base/quadgk.jl\">quadgk</a> is a function in the <code>QuadGK</code> package  provided to compute one-dimensional integrals. We have:</p>","metadata":{}},
{"cell_type":"markdown","source":"<ul>\n<li><p><code>quad</code> is for quadrature, an old-term now meaning integrals</p>\n</li>\n<li><p><code>g</code> is Gauss</p>\n</li>\n<li><p><code>k</code> is Kronrod</p>\n</li>\n</ul>","metadata":{}},
{"cell_type":"markdown","source":"<p>The <a href=\"https://en.wikipedia.org/wiki/Gauss&#37;E2&#37;80&#37;93Kronrod_quadrature_formula\">Kronrod</a> part is a modification to the choice of Gauss points.</p>","metadata":{}},
{"cell_type":"markdown","source":"<p>The <code>quadgk</code> function is adaptive, in that it compute an estimated integral over a region. If the error is not small enough, it subdivides that region and tries again on each subdivision. The Kronrod part makes the computation over subdivisions more efficient.</p>","metadata":{}},
{"cell_type":"markdown","source":"<p>The function returns <em>both</em> an answer and an error estimate:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/plain":["(2.0, 1.7905676941154525e-12)"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["using QuadGK\nQuadGK.quadgk(sin, 0, pi)"],"metadata":{},"execution_count":1},
{"cell_type":"markdown","source":"<p>Here the <em>exact</em>  answer of $2$ is found, but the algorithm used estimates the error by <code>1e-12</code>. (As the error is 0, this is a good bound, though larger than needed.)</p>","metadata":{}},
{"cell_type":"markdown","source":"<p>Other integrals are possible:</p>","metadata":{}},
{"outputs":[{"output_type":"execute_result","data":{"text/plain":["(2.925303491814363, 2.177147351289932e-11)"]},"metadata":{},"execution_count":1}],"cell_type":"code","source":["QuadGK.quadgk(x->exp(x^2), -1, 1)"],"metadata":{},"execution_count":1}
    ],
 "metadata": {
  "language_info": {
   "file_extension": ".jl",                                                                                                             
   "mimetype": "application/julia", 
   "name": "julia",
   "version": "0.6"
  },
 "kernelspec": {
   "display_name": "Julia 0.6.0",
   "language": "julia",
   "name": "julia-0.6"
  }

 },
 "nbformat": 4,
 "nbformat_minor": 2

}
